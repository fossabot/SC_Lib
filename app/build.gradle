plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

ext {
    // group id
    PUBLISH_GROUP_ID = 'io.github.danilerofei'
    // текущая версия библиотеки
    PUBLISH_VERSION = '0.0.1'
    // artifact id библиотеки
    PUBLISH_ARTIFACT_ID = 'SC-lib'
}

configurations.implementation.setCanBeResolved(true)
configurations.api.setCanBeResolved(true)

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    namespace 'com.samplecode.lib'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles "consumer-rules.pro"
        }
        debug {
            consumerProguardFiles "consumer-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs += ['-module-name', "com.samplecode.lib", "-Xjvm-default=all"]
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
        exclude('META-INF/kotlinx_coroutines_core.version')
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/octocat/sc-lib"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

task copyLibs(type: Copy) {
    from configurations.implementation
    into 'libs'
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.11'
    implementation 'com.google.code.gson:gson:2.10.1'
}